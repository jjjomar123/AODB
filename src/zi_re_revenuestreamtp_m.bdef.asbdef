managed implementation in class zbp_i_re_revenuestreamtp_m unique;
strict ( 2 );
with draft;

define behavior for ZI_RE_RevenueStreamTP_M alias Header
persistent table zre_rvnstreamhdr
draft table zre_rvnstreamhdd
with additional save
lock master
total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  field ( readonly, numbering : managed ) Streamuuid;
  field ( readonly ) LocalCreatedAt, LocalCreatedBy, LastChangedAt, LocalLastChangedAt, LocalLastChangedBy;
  field ( mandatory ) Companycode, Code;

  association _Item { create; with draft; }

  determination setDefaultitem on modify { create; }

  validation revenuekey on save { create; field Companycode, Code; }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare{
    validation revenuekey;
    validation Roundoff~sequencekey;
    validation Roundoff~roundoffRange;
    validation Consideration~validityrange;
    validation Consideration~validentryexists;

  }

  mapping for zre_rvnstreamhdr corresponding ;
}

define behavior for ZI_RE_RevenueStreamItemTP_M alias Item
persistent table zre_rvnstrmitem
draft table zre_rvnstrmitemd
lock dependent by _Root
authorization dependent by _Root
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) Streamuuid, Nature, Locallastchangedat;
  field ( readonly, numbering : managed ) Streamitemuuid;
  association _Root { with draft; }
  association _Calcrule { create; with draft; }
  association _Consideration { create; with draft;  }
  association _Round { create; with draft; }

  mapping for zre_rvnstrmitem corresponding ;
}

define behavior for ZI_RE_RevenueStreamCALCRU_TP_M alias CalculationRule
persistent table zre_rvnamrcalcr
draft table zre_rvnamrcalcrd
lock dependent by _Root
authorization dependent by _Root
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) Streamuuid, Streamitemuuid, Locallastchangedat;
  field ( readonly, numbering : managed ) Calcruleuuid;
  association _Item { with draft; }
  association _Root { with draft; }

  mapping for zre_rvnamrcalcr corresponding ;
}

define behavior for ZI_RE_RevenueStreamCNSTP_M alias Consideration
persistent table zre_rvnstmconsd
draft table zre_rvnstmconsdd
lock dependent by _Root
authorization dependent by _Root
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) Streamuuid, Streamitemuuid, Status, Locallastchangedat, Baseunitofmeasure;
  field ( readonly, numbering : managed ) Cnsderationuuid;

  validation validityrange on save { create; update; field Validfrom, Validto; }
  validation validentryexists on save { create; update; field Validfrom, Validto; }

  association _Item { with draft; }
  association _Root { with draft; }

  mapping for zre_rvnstmconsd corresponding ;
}

define behavior for ZI_RE_RevenueStreamRNDTP_M alias Roundoff
persistent table zre_rvnstmround
draft table zre_rvnstmroundd
lock dependent by _Root
authorization dependent by _Root
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( readonly ) Streamuuid, Streamitemuuid, Locallastchangedat, Baseunitofmeasure;
  field ( readonly, numbering : managed ) Roundoffuuid;

  validation sequencekey on save { create; update; field Rndsequence; }
  validation roundoffRange on save { create; update; field Roundfrom, Roundto; }

  association _Item { with draft; }
  association _Root { with draft; }

  mapping for zre_rvnstmround corresponding ;
}